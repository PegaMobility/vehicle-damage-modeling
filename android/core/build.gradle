apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

project.group = 'com.pega.mobile.vehicle-damage-modeling.android'
project.version = '1.0'

publishing {
    artifactoryPublish.dependsOn('jacocoTestReport')
    publications {
        core(MavenPublication) {
            groupId = project.group
            version = project.version
            artifactId = 'core'
            from components.java
        }
    }
}


artifactory {
    contextUrl = 'https://pegasystemskrakow.jfrog.io/pegasystemskrakow/'
    publish {
        repository {
            repoKey = 'VehicleDamageModelingMaven'
            if (project.hasProperty('jFrogUser') && project.hasProperty('jFrogPassword')) {
                username = project.property('jFrogUser')
                password = project.property('jFrogPassword')
            }
        }
        defaults {
            publishArtifacts = true
            publishPom = true
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.core)
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'junit:junit:4.12'

    testImplementation 'org.mockito:mockito-core:2.25.1'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport